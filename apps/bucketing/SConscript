Import('fgEnv', 'cgEnv')

# [mcj] Note that the equivalent Makefile only conditionally defines
# these flags. Are we sure we want 64- or 32-bit values/weights?
# TODO document why these should be enabled in Julienne but not Ligra
# TODO decide about -DPD -DBYTE -DNIBBLE
fgEnv.Append(CPPDEFINES = ['LONG', 'EDGELONG', 'BYTERLE'])
cgEnv.Append(CPPDEFINES = ['LONG', 'EDGELONG', 'BYTERLE'])

apps = [
        "KCore",
        "SetCover"
        ]

cgEnv.Append(CPPDEFINES = ['COARSE_GRAIN'])

relaxedEnv = fgEnv.Clone()
relaxedEnv.Append(CPPDEFINES = ['RELAXED'])
relaxedEnv['PROGSUFFIX'] = '_relaxed'
relaxedEnv['OBJSUFFIX'] = '.ro'
relaxedEnv.Program(target='plsSetCover', source=['SetCover_pls.C'])

naEnv = relaxedEnv.Clone()
naEnv.Append(CPPDEFINES = ['NONATOMIC_TASKS'])
naEnv['PROGSUFFIX'] = '_na'
naEnv['OBJSUFFIX'] = '.no'

for env in [fgEnv, cgEnv, naEnv]:
    for app in apps:
        env.Program(target='pls' + app, source=[app + '_pls.C'])

